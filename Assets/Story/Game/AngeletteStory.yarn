title: Start
tag:
---

<<declare $score = 0>>
<<set $score = 0>>

<<HideAngelette AngelicAnimations>>
<<Set GameBackground Cafecito1_bg>>
<<SetVolume Sound 0>>
<<PlayMusic Sound track01>>
<<FadeInMusic Sound 3>>
<<FadeOut Fade 3>>
<<wait 3>>

???: Hola??
???: Soy Angelette,
<<ShowAngelette AngelicAnimations>>
<<SetNeutralAnimation AngelicAnimations>>
Angelette: De tinder!
Su belleza es literalmente cegadora.
Angelette: Me estas esperando a mi, o no?

-> Sí! // [5]
    <<set $score = $score + 5>>
    Angelette: Jiji, Hola!!
    Angelette: Te ves mejor en persona!
    Angelette: Me puse un poquito nerviosa, jijiji.
-> Sí? // [2]
    <<set $score = $score + 2>>
    Angelette: oh... yo... em....
    Angelette: Supongo que me veo un poquito distinta en persona...

Angelette: Disculpa por llegar tarde!
Angelette: Ai odio esto es que mi jefe es pésimo y siempre estoy haciendo horas extra!
Angelette: Igual me quería arreglar antes de venir...
Angelette: Quizás el delineado fue mucho o no...
Angelette: Te gusta mi delineado??

-> sí... // [0]
    <<set $score = $score + 0>>
    <<PauseMusic Sound>>
    <<Shake ShakeEffect>>
    <<PlaySFX Sound record-scratch-sound2>>
    <<SetShockAnimation AngelicAnimations>>
    Angelette: ...
    Angelette: ...
    <<ResumeMusic Sound>>
    Angelette: Bueno.
-> Sí! // [3]
    <<set $score = $score + 3>>
    <<SetBlushOn AngelicAnimations true>>
    Angelette: Ai, para!
    Angelette: Jijiiji, Que como que me da vergüenza...
    <<SetBlushOn AngelicAnimations false>>
-> Sí. // [5]
    <<set $score = $score + 5>>
    <<SetBlushOn AngelicAnimations true>>
    Angelette: Oooh...
    Angelette: Eres de perso seria y decidida, o no?
    Angelette: Me gusta, jiji.
    <<SetBlushOn AngelicAnimations false>>

<<SetNeutralAnimation AngelicAnimations>>
Angelette: No tenía mucho tiempo en verdad...
Angelette: No se si te ha tocado trabajar con la familia.
Angelette: Son todos unos metiches que andan sapeando todo el rato!
Angelette: Imagínate mi jefe es mi papá, terrible...
Angelette: Deberíamos pedir algo o no?
Angelette: ...

<<FadeIn Fade 2>>
<<wait 2>>
<<Set GameBackground Cafecito2_bg>>
<<FadeOut Fade 2>>
<<wait 2>>

// ### El cafe, mesa post-comida.

Angelette: ... verdaderamente terrible.
Angelette: Es que no entiendo porqué...
Angelette: Me hacen escoger entre Olivia Rodrigo y Sabrina Carpenter!
Angelette: Cuando todo es culpa de un tipo random!! Que ni siquiera es tan famoso!
Angelette: La humanidad es tan cruel...
Angelette: Porqué tenemos que poner a dos chicas talentosas una contra la otra?!
Angelette: Me entiendes?

-> .... sí..... // [0]
    <<set $score = $score + 0>>
    <<SetSadAnimation AngelicAnimations>>
    Angelette: jaja, que poco convincente tu respuesta...
    Angelette: tanto te estoy aburriendo?
    <<SetNeutralAnimation AngelicAnimations>>
    Angelette: Tranqui, podemos hablar de otra cosa...
-> sí, sí... // [5]
    <<set $score = $score + 5>>
    Angelette: Yo sabía que ibas a estar de acuerdo!
    <<SetBlushOn AngelicAnimations true>>
    Angelette: Jijijiji.
    <<SetBlushOn AngelicAnimations false>>
-> Sí! // [2]
    <<set $score = $score + 2>>
    Angelette: Jaja, ya...
    Angelette: no tienes que mentirme tampoco.

Angelette: Uff igual llevamos caleta de rato acá o no?
Angelette: en... disculpa!
Angelette: La cuenta!!

<<FadeIn Fade 2>>
<<wait 2>>
<<Set GameBackground Calle_bg>>
<<FadeOut Fade 2>>
<<wait 2>>

// ## Scene 02 / “La Calle”

Angelette: La he estado pasando super,
Angelette: Tinder es super loco,
Angelette: Uno nunca sabe con que se encuentra...
<<SetBlushOn AngelicAnimations true>>
Angelette: Tú...
Angelette: Habrías aceptado salir conmigo si yo fuera una cosa rara...
Angelette: Saldrías conmigo si fuera un gusanito?

-> Sí. // [5]
    <<set $score = $score + 5>>
    Angelette: Jijiji
    Angelette: En verdad eres una persona decidida.
    Angelette: No me pasa muy seguido...
-> Sí? // [0]
    <<set $score = $score + 0>>
    <<SetBlushOn AngelicAnimations false>>
    <<PauseMusic Sound>>
    Angelette: ...
    Angelette: ...
    <<ResumeMusic Sound>>
    Angelette: Te hice una pregunta rara o no?
    Angelette: Jaja...

<<SetBlushOn AngelicAnimations false>>
Angelette: Sabes qué?
Angelette: Estoy un poquito cansada.

<<if $score <= 5>>
    // ## Final C [0~5]
    <<FadeOutMusic Sound 3>>
    <<FadeIn Fade 3>>
    <<wait 3>>
    <<HideAngelette AngelicAnimations>>
    <<Set GameBackground Black>>
    <<FadeOut Fade 1>>
    <<wait 1>>

    Angelette se despidió de ti... y tú volviste a tu casa.
    Al día siguiente, le mandaste un mensaje...
    ”La pasé bien el otro día, podríamos volver a salir pronto.”
    Ella te dejó el visto
    Y no te habló más.
<<else>>
    Angelette: En verdad vivo super cerca de aquí...
    Angelette: Nose...
    <<SetBlushOn AngelicAnimations true>>
    Angelette: Si te gustaría podríamos seguir en mi depa...

    <<FadeIn Fade 2>>
    <<wait 2>>
    <<Set GameBackground Depa_bg>>
    <<FadeOut Fade 2>>
    <<wait 2>>

    <<SetBlushOn AngelicAnimations false>>
    Es lindo el depa, tiene buen gusto para el decor...

    <<SetNeutralAnimation AngelicAnimations>>
    Angelette: Bienvenido!
    Angelette: Te hago un cafecito?
    Angelette: Yo...
    Angelette: Te juro que no invito a todo el mundo a mi departamento así como así nomas...
    Angelette: Me crees o no?

    -> Sí... // [5]
        <<set $score = $score + 5>>
        <<SetBlushOn AngelicAnimations true>>
        Angelette: Jiji...
        Angelette: Yo sabía que me ibas a responder eso...
        <<SetBlushOn AngelicAnimations false>>
    -> Sí! // [2]
        <<set $score = $score + 2>>
        <<Shake ShakeEffect>>
        Angelette: !!
        Angelette: Puedes ser medio intenso tú...
        <<SetBlushOn AngelicAnimations true>>
        Angelette: No me desagrada...
        <<SetBlushOn AngelicAnimations false>>

    Angelette: Sabes qué?
    Angelette: Puedes quedarte un ratito más...
    Angelette: Si quieres.

    -> Sí.
        <<if $score <= 14>>
            // ## Final B [6~14]
            <<FadeOutMusic Sound 3>>
            <<FadeIn Fade 3>>
            <<wait 3>>
            <<HideAngelette AngelicAnimations>>
            <<Set GameBackground Black>>
            <<FadeOut Fade 1>>
            <<wait 1>>
            Después de varias citas,
            Tú y Angelette empezaron una relación,
            Como todas, con altos y bajos
            Después de un año y medio...
            Ella se ganó una beca y se marchó a Noruega.
            Terminaron en buenos términos,
            Pero no la viste más...
        <<else>>
            // ## Final A [15~25]
            <<FadeOutMusic Sound 3>>
            <<FadeIn Fade 3>>
            <<wait 3>>
            <<HideAngelette AngelicAnimations>>
            <<Set GameBackground Black>>
            <<FadeOut Fade 1>>
            <<wait 1>>
            Tú y Angelette empezaron una relación bonita y emocionante.
            Ella hizo un post-doctorado en finanzas y consiguió el ascenso de sus sueños,
            Se mudaron juntos y vivieron felices por 5 años...
            En su aniversario número 6, Angelette te pidió que te casaras con ella,
            Tú dijiste “Sí”.
        <<endif>>
<<endif>>


<<FadeIn Fade 3>>
<<wait 3>>
<<GameOver GameManager>>
===